10 ' *******************************  
20 ' RPG GAME - init
30 ' by @dfreniche
40 ' July 2019
50 '
60 ' This file contains initialization
70 ' code for the game
80 ' *******************************

100 ' INITIALIZE
110 ' Define all variables as integers
120 DEFINT a-z

200 ' PRESENTATION
210 MODE 1
220 ' DEFINE SPRITES
230 GOSUB 1300
240 ' PRINT PRESENTATION FROM FILE
250 GOSUB 1000
260 ' PRINT LINE OF SPRITES
270 GOSUB 1100
280 PRINT"PRESS ENTER TO START"
290 INPUT"",a$
300 CLS

400 ' LOAD MAIN FILE
410 run"main"

999 ' PRINT PRESENTATION FROM FILE
1000 OPENIN "PRESENT.TXT"
1010 WHILE NOT EOF
1020 INPUT #9, l$: ?l$
1030 WEND
1040 CLOSEIN
1050 RETURN

1099 ' PRINT LINE OF SPRITES
1100 FOR N=1 TO 40
1110 IF N MOD 2=0 THEN ?CHR$(250); ELSE ?CHR$(251);
1120 NEXT N
1130 ?""
1140 RETURN

1299 ' SPRITE DEFINITION
1300 SYMBOL AFTER 250
1310 ' ENEMY
1320 SYMBOL 250,56,108,84,238,198,186,254,170
1330 ' MONSTER
1340 SYMBOL 251,56,59,59,18,126,18,124,68
1350 RETURN

10000 ' LEAVE THIS EMPTY LINE AT THE END FOR UNIX2DOS
